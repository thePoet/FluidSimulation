// Compute shader for particle based fluid dynamics.
// Based on paper by Simon Clavet, Philippe Beaudoin, and Pierre Poulin
// https://www.academia.edu/452554/Particle-Based_Viscoelastic_Fluid_Simulation
#include "FluidDynamicsCommon.hlsl"

#pragma kernel InitAndApplyGravity                         
#pragma kernel ClearPartitioningCells              
#pragma kernel FillPartitioningCells                
#pragma kernel FindNeighbours                      
#pragma kernel CalculateViscosity                   
#pragma kernel ApplyViscosity                      
#pragma kernel ApplyVelocity                        
#pragma kernel CalculatePressures                   
#pragma kernel CalculateDensityDisplacement         
#pragma kernel MoveParticles


struct Particle
{
    float2 Position;
    float2 Velocity;
    int fluidIdx;
    int active;
};

struct TempParticleData
{
    float2 DisplacementCollision;
    float2 VelocityChange;
    float2 PreviousPosition;
    float Pressure;
    float NearPressure;
    float2 DisplacementVelocity;
    float2 Unused2;
    float2 DisplacementDensity;
};

struct Fluid
{
    int State; // 0 = liquid, 1 = gas, 2 = solid
    float Stiffness;
    float NearStiffness;
    float RestDensity;
    float ViscositySigma;
    float ViscosityBeta;
    float GravityScale;
    float Mass;
    float DensityPullFactor;
};

struct ProximityAlert
{
    int particleIdx1;
    int particleIdx2;
};

struct Variables
{
    int numProximityAlerts;
    int numCellOverflowErrors;
    int numParticleOutsideAreaErrors;
    int numInsideSolidWarnings;
    int proximityAlertsMaxedOut;
};

// Settings
//int _NumParticles;
int _MaxNumParticles=0;
int _MaxNumNeighbours;
int _MaxNumParticlesPerCell;
int _MaxNumProximityAlerts;

float _InteractionRadius;
float _Gravity;
float _Drag;
float _SolidRadius;
float _MaxDisplacement;

float _AreaMinX;
float _AreaMinY;
float _AreaMaxX;
float _AreaMaxY;

float _DeltaTime;

int _SelectedParticle; // For getting debug info

float _Time = 0;
int _NumFluids;

static const float EPSILON = 0.00001;


//TODO: RWStructured ei liene tehokkain tyyppi näille kaikille:

RWStructuredBuffer<Particle> _Particles; //Length: MaxNumParticles
RWStructuredBuffer<TempParticleData> _TempData; 
// Number of cells: ceil((AreaMaxX - AreaMinX) / InteractionRadius) * ceil((AreaMaxY - AreaMinY) / InteractionRadius)
RWStructuredBuffer<int> _CellParticleCount; // Length: Number of cells
RWStructuredBuffer<int> _ParticlesInCells; //Length:  Number of cells * MaxNumParticlesPerCell
RWStructuredBuffer<int> _ParticleNeighbours; //Length: MaxNumParticles * MaxNumNeighbours
RWStructuredBuffer<int> _ParticleNeighbourCount; //Length: MaxNumParticles 
//RWStructuredBuffer<int> _Stats; //Length: 10 
RWStructuredBuffer<float2> _Debug; //Length: 5 
RWStructuredBuffer<Fluid> _Fluids; 
RWStructuredBuffer<float> _ProximityAlertMatrix; 
RWStructuredBuffer<ProximityAlert> _ProximityAlerts;
RWStructuredBuffer<Variables> _Variables;


// --------------------- UTILITY FUNCTIONS ---------------------

// Distance between particles compared to interaction radius 0..1
float QDistance(int particle1, int particle2)
{
    return length(_Particles[particle1].Position - _Particles[particle2].Position) / _InteractionRadius;
}

Fluid FluidOf(int particleIdx)
{
    return _Fluids[_Particles[particleIdx].fluidIdx];
}

bool IsLiquid(int particleIdx)
{
    return FluidOf(particleIdx).State == 0;
}

bool IsGas(int particleIdx)
{
    return FluidOf(particleIdx).State == 1;
}

bool IsSolid(int particleIdx)
{
    return FluidOf(particleIdx).State == 2;
}

bool IsFluid(int particleIdx)
{
    return FluidOf(particleIdx).State != 2;
}

float ProximityAlertDistance(int particleIdx1, int particleIdx2)
{
    int matrixIdx = _Particles[particleIdx1].fluidIdx + _Particles[particleIdx2].fluidIdx * _NumFluids;
    return _ProximityAlertMatrix[matrixIdx];
}




// ---------------------------------

bool IsOutsideArea(float2 position)
{
    float2 minCorner = float2(_AreaMinX, _AreaMinY);
    float2 maxCorner = float2(_AreaMaxX, _AreaMaxY);
    return IsInsideBox(position, minCorner, maxCorner); 
}

float2 AreaBoundsDisplacement(in float2 particlePosition, out float addedDrag)
{
    float2 result = float2(0.0, 0.0);
    float margin = 10;
    addedDrag = 0;
    
    float2 minCornerSoft = float2(_AreaMinX + margin, _AreaMinY + margin);
    float2 maxCornerSoft = float2(_AreaMaxX - margin, _AreaMaxY - margin);

    if (!IsInsideBox(particlePosition, minCornerSoft, maxCornerSoft))
    {
        addedDrag = 0.5;
        float softness = 0.5;
        float2 newPos = ClampToBox(particlePosition, float2(_AreaMinX+EPSILON,_AreaMinY+EPSILON), float2(_AreaMaxX-EPSILON,_AreaMaxY-EPSILON));

        if (newPos.x < minCornerSoft.x)
        {
            newPos.x = lerp(newPos.x, minCornerSoft.x, softness);
        }
        if (newPos.x > maxCornerSoft.x)
        {
            newPos.x = lerp(newPos.x, maxCornerSoft.x, softness);
        }
        if (newPos.y < minCornerSoft.y)
        {
            newPos.y = lerp(newPos.y, minCornerSoft.y, softness);
        }
        if (newPos.y > maxCornerSoft.y)
        {
            newPos.y = lerp(newPos.y, maxCornerSoft.y, softness);
        }
        result = newPos - particlePosition;
    }

    return result;
}



// TODO: törmäyksen normaali tai nopeuskorjaus ulos tästä funktiosta



bool TestSolidCollisions(in int particleIdx, in float2 start, in float2 goal, out float2 reached, out float2 collisionNormal)
{
    bool collisionHappened = false;
    reached = goal;
    float2 collisionPoint;

    
    for (int j=0; j<_ParticleNeighbourCount[particleIdx]; j++)
    {
        int neighbourIndex = _ParticleNeighbours[particleIdx * _MaxNumNeighbours + j];
        if (!IsSolid(neighbourIndex) ) continue;

        float2 neighbourPos = _Particles[neighbourIndex].Position;


        //TODO:mitä jos ollaan samaan aikaan kahden sisällä?
        // TODO: epsilon-case
        float2 toNeighbour = neighbourPos-start; 
        if (length(toNeighbour) < _SolidRadius)
        {
            // If we start inside particle, we'll take shortest route out:
            _Variables[0].numInsideSolidWarnings++;
            reached = neighbourPos - normalize(toNeighbour) * (_SolidRadius + EPSILON);
            collisionNormal = -toNeighbour;
            collisionHappened = false; //?????
            break;
        }
        
        if (LineSegmentCircleIntersection(neighbourPos, _SolidRadius, start, reached, collisionPoint))
        {
            float margin = _SolidRadius/80.0;
            collisionNormal = normalize(collisionPoint-neighbourPos);
            // By moving the particle a little bit along the collision normal we ensure it does not get stuck on a slope
            reached = collisionPoint + margin * collisionNormal; 
            collisionHappened = true;
        }
    }
    return collisionHappened;

}

// --------------------- GRAVITY------------------------------------------ ---------------------
[numthreads(8,8,1)]
void InitAndApplyGravity (uint3 id : SV_DispatchThreadID) 
{
    int pIdx = id.x * 128 + id.y;

    if (pIdx==0)
    {
        _Time += _DeltaTime;

        _Variables[0].numProximityAlerts=0;
        _Variables[0].numCellOverflowErrors=0;
        _Variables[0].numInsideSolidWarnings=0;
        _Variables[0].numParticleOutsideAreaErrors=0;
        _Variables[0].proximityAlertsMaxedOut=0;
        
        for (int i=0; i<_MaxNumProximityAlerts; i++)
        {
            _ProximityAlerts[i].particleIdx1 = 0;
            _ProximityAlerts[i].particleIdx2 = 0;
        }
    }

    if (pIdx < 0 || pIdx >= _MaxNumParticles) return;
    if (_Particles[pIdx].active == 0) return;

    _TempData[pIdx].PreviousPosition = _Particles[pIdx].Position;
    _TempData[pIdx].DisplacementCollision = float2(0, 0);
    _TempData[pIdx].VelocityChange = float2(0, 0);
    _TempData[pIdx].DisplacementDensity = float2(0, 0);
    _TempData[pIdx].DisplacementVelocity = float2(0, 0);
    _TempData[pIdx].Unused2 = float2(0, 0);
    

    // Apply gravity to fluid particles:
    if (IsSolid(pIdx)) return;
    _Particles[pIdx].Velocity += float2(0.0, -_DeltaTime * _Gravity * FluidOf(pIdx).GravityScale);
}

// ------------- SPATIAL PARTITIONING ----------------------------------------------------------------
int2 CellCoordinates(in float2 position)
{
    int2 result;
    result.x = floor((position.x-_AreaMinX)/_InteractionRadius);
    result.y = floor((position.y-_AreaMinY)/_InteractionRadius);
    return result;
}

int CellIndex(in float2 position)
{
    int index = -1;
    
    if (position.x >= _AreaMinX && position.x <= _AreaMaxX && position.y >= _AreaMinY && position.y <= _AreaMaxY)
    {
        int2 coords = CellCoordinates(position);
        index = coords.x + coords.y * ceil((_AreaMaxX-_AreaMinX)/_InteractionRadius);
    }
    return index;
}

bool IsInsideArea(float2 position)
{
    return position.x >= _AreaMinX && position.x <= _AreaMaxX && position.y >= _AreaMinY && position.y <= _AreaMaxY;
}

int NumCells()
{
    return ceil((_AreaMaxX-_AreaMinX)/_InteractionRadius) *
        ceil((_AreaMaxY-_AreaMinY)/_InteractionRadius);
}





[numthreads(8,8,1)]
void ClearPartitioningCells (uint3 id : SV_DispatchThreadID)
{
    int index = id.x * 128 + id.y;
    if (index >= NumCells()) return;
    _CellParticleCount[index] = 0;

    // Set error flags to zero
/*
    if (index==0)
    {
        for (int i=0; i<10; i++)
            _Stats[i] = 0;
    }*/
}


[numthreads(8,8,1)]
void FillPartitioningCells (uint3 id : SV_DispatchThreadID)
{
    int pIdx = id.x * 128 + id.y;
    if (pIdx < 0 || pIdx >= _MaxNumParticles) return;
    if (_Particles[pIdx].active == 0) return;


    const int cellIndex = CellIndex(_Particles[pIdx].Position);

    if (cellIndex==-1)
    {
        _Variables[0].numParticleOutsideAreaErrors++;
        return;
    }
  
    int previousCount = 0;
    InterlockedAdd(_CellParticleCount[cellIndex], 1, previousCount);
    if (previousCount >= _MaxNumParticlesPerCell)
    {
        _Variables[0].numCellOverflowErrors++;
        return;
    }

    _ParticlesInCells[cellIndex * _MaxNumParticlesPerCell + previousCount] = pIdx;
}

// ----------------- Neighbour Finding -----------------


[numthreads(8,8,1)]
void FindNeighbours (uint3 id : SV_DispatchThreadID)
{
    int pIdx = id.x * 128 + id.y;
    if (pIdx < 0 || pIdx >= _MaxNumParticles) return;
    if (_Particles[pIdx].active == 0) return;

    
    int count = 0;
   
    for (int x=-1; x<=1; x++)
    {
        for (int y=-1; y<=1; y++)
        {
            int cellIndex = CellIndex(_Particles[pIdx].Position + float2(x*_InteractionRadius, y*_InteractionRadius));
            if (cellIndex == -1) continue;

            for (int i=0; i<_CellParticleCount[cellIndex]; i++)
            {
                if (i==_MaxNumParticlesPerCell) break;

                int otherParticleIndex = _ParticlesInCells[cellIndex * _MaxNumParticlesPerCell + i];
                if (otherParticleIndex == pIdx) continue;

                float distance = length(_Particles[pIdx].Position - _Particles[otherParticleIndex].Position);
                if (distance < _InteractionRadius)
                {
                    _ParticleNeighbours[pIdx * _MaxNumNeighbours + count] = otherParticleIndex;
                    count = count + 1;
                }

                float alertDistance = ProximityAlertDistance(pIdx, otherParticleIndex);
           
                if (alertDistance > 0 && distance < alertDistance)
                {
                    if (_Variables[0].numProximityAlerts >= _MaxNumProximityAlerts)
                    {
                        _Variables[0].proximityAlertsMaxedOut = 1;
                        continue;
                    }
                    int previousNum;
                    InterlockedAdd(_Variables[0].numProximityAlerts, 1, previousNum);
                    _ProximityAlerts[previousNum].particleIdx1 = pIdx;
                    _ProximityAlerts[previousNum].particleIdx2 = otherParticleIndex;
                }
            }
        }
    }
    _ParticleNeighbourCount[pIdx] = count;
}


bool IsInDifferentState(int particle1, int particle2)
{
    return _Fluids[_Particles[particle1].fluidIdx].State != _Fluids[_Particles[particle2].fluidIdx].State;
}


float2 ViscosityImpulse(int particle1, int particle2)
{
    float2 impulse = float2(0,0);
    
    if (!IsSolid(particle1) && !IsSolid(particle2))// && !IsInDifferentState(particle1, particle2) )
    {
        int fluidIdx = _Particles[particle1].fluidIdx;
        float sigma = _Fluids[fluidIdx].ViscositySigma;
        float beta = _Fluids[fluidIdx].ViscosityBeta;


        float q = QDistance(particle1, particle2);

        if (q>0.000001 && q < 1)
        {
            float2 r = normalize(_Particles[particle1].Position - _Particles[particle2].Position);
            float u = dot(_Particles[particle1].Velocity - _Particles[particle2].Velocity, r);
            if (u > 0)  impulse =  _DeltaTime * (1 - q) * (sigma * u + beta * u * u) * r;
        }
    }

    return impulse;
}


[numthreads(8,8,1)]
void CalculateViscosity (uint3 id : SV_DispatchThreadID)
{
    int pIdx = id.x * 128 + id.y;
    if (pIdx < 0 || pIdx >= _MaxNumParticles) return;
    if (_Particles[pIdx].active == 0) return;

    
    for (int i=0; i<_ParticleNeighbourCount[pIdx]; i++)
    {
        int neighbourIndex = _ParticleNeighbours[pIdx * _MaxNumNeighbours + i];
        float2 impulse = ViscosityImpulse(pIdx, neighbourIndex);
        _TempData[pIdx].VelocityChange += -impulse * 0.5;
   }
}

[numthreads(8,8,1)]
void ApplyViscosity (uint3 id : SV_DispatchThreadID)
{
    int pIdx = id.x * 128 + id.y;
    if (pIdx < 0 || pIdx >= _MaxNumParticles) return;
    if (_Particles[pIdx].active == 0) return;

    _Particles[pIdx].Velocity += _TempData[pIdx].VelocityChange;
}

[numthreads(8,8,1)]
void ApplyVelocity (uint3 id : SV_DispatchThreadID)
{
    int pIdx = id.x * 128 + id.y;
    if (pIdx < 0 || pIdx >= _MaxNumParticles) return;
    if (_Particles[pIdx].active == 0) return;

    if (IsSolid(pIdx)) return;

    _TempData[pIdx].DisplacementVelocity = _Particles[pIdx].Velocity * _DeltaTime;
}

[numthreads(8,8,1)]
void CalculatePressures (uint3 id : SV_DispatchThreadID)
{
    int pIdx = id.x * 128 + id.y;
    if (pIdx < 0 || pIdx >= _MaxNumParticles) return;
    if (_Particles[pIdx].active == 0) return;


    int fluidIndex = _Particles[pIdx].fluidIdx;
 
    const float restDensity = _Fluids[fluidIndex].RestDensity;
    const float stiffness = _Fluids[fluidIndex].Stiffness;
    const float nearStiffness = _Fluids[fluidIndex].NearStiffness;

    float density = 0;
    float nearDensity = 0;
    
    for (int i=0; i<_ParticleNeighbourCount[pIdx]; i++)
    {
        int neighbourIndex = _ParticleNeighbours[pIdx * _MaxNumNeighbours + i];

        if (IsSolid(neighbourIndex)) continue;
        float q = QDistance(pIdx, neighbourIndex);
        if (q<1)
        {
            // Visual density, does not take mass in to account
            density += (1-q) * (1-q);
            nearDensity += (1-q) * (1-q) * (1-q);   
        }
    }
    _TempData[pIdx].Pressure = stiffness * (density - restDensity);
    _TempData[pIdx].NearPressure = nearStiffness * nearDensity;
}

// Returns the movement caused by fluid density at particle1
// Direction towards particle2 if pressure at particle1 is high.
float2 DensityDisplacement(int particle1, int particle2, float pullFactor=1)
{
    float q = QDistance(particle1, particle2);
    float2 fromTo = _Particles[particle2].Position - _Particles[particle1].Position;

    float2 displacement = float2(0.0, 0.0);

    if (length(fromTo) < EPSILON) // If particles are extremely close...
    {
        // ...move them in random direction.
        displacement =  RandomFloat2(float2(particle1, _Time)) * 2 * EPSILON;
    }
    else if (q<1)
    {
        float pressure = _TempData[particle1].Pressure;
        float nearPressure = _TempData[particle1].NearPressure;
   
        float2 d = _DeltaTime * _DeltaTime * (pressure * (1 - q) + nearPressure * (1 - q) * (1 - q)) *
                        normalize(fromTo);

        float massFactor = max(FluidOf(particle1).Mass / FluidOf(particle2).Mass, 1);
        
        displacement =  0.5 * massFactor * d;

        if (pressure < 0) displacement *= pullFactor;
        
    }
    return displacement;
}


[numthreads(8,8,1)]
void CalculateDensityDisplacement (uint3 id : SV_DispatchThreadID)
{
    int pIdx = id.x * 128 + id.y;
    if (pIdx < 0 || pIdx >= _MaxNumParticles) return;
    if (_Particles[pIdx].active == 0) return;
    
    if (IsSolid(pIdx) ) return;
    
    for (int j=0; j<_ParticleNeighbourCount[pIdx]; j++)
    {
        int neighbourIdx = _ParticleNeighbours[pIdx * _MaxNumNeighbours + j];

        if (IsFluid(neighbourIdx) )
        {
            float pullFactor = FluidOf(pIdx).DensityPullFactor;
            if (_Particles[pIdx].fluidIdx != _Particles[neighbourIdx].fluidIdx)
            {
                pullFactor = 0;
            }
            
            _TempData[pIdx].DisplacementDensity -= DensityDisplacement(pIdx, neighbourIdx, pullFactor);
            _TempData[pIdx].DisplacementDensity += DensityDisplacement(neighbourIdx, pIdx, pullFactor);
        }
    }
}


[numthreads(8,8,1)]
void MoveParticles(uint3 id : SV_DispatchThreadID)
{
    int pIdx = id.x * 128 + id.y;
    if (pIdx < 0 || pIdx >= _MaxNumParticles) return;
    if (_Particles[pIdx].active == 0) return;

    
    if (IsSolid(pIdx)) return;

    float2 reached;
    float2 positionChange = _TempData[pIdx].DisplacementVelocity + _TempData[pIdx].DisplacementDensity;
    positionChange = CapMagnitude(positionChange, _MaxDisplacement);
    float2 collisionNormal;
    float2 preCollisionVelocity = positionChange / _DeltaTime;
    
    bool collisionHappened = TestSolidCollisions(pIdx, _Particles[pIdx].Position, 
        _Particles[pIdx].Position + positionChange,
        reached, collisionNormal);

    _TempData[pIdx].DisplacementCollision = reached - (_Particles[pIdx].Position + positionChange);
    
    float addedDrag;
    
    _Particles[pIdx].Position = reached;
    
    _Particles[pIdx].Position += AreaBoundsDisplacement(_Particles[pIdx].Position, addedDrag);
    
    _Particles[pIdx].Velocity = (_Particles[pIdx].Position - _TempData[pIdx].PreviousPosition) / _DeltaTime;
    if (collisionHappened)
    {
        _Particles[pIdx].Velocity = VelocityAfterCollision(preCollisionVelocity, collisionNormal, 0.05, 0.95);
    }

    // Apply Drag
    if (length(_Particles[pIdx].Velocity) < 0.0001) return;
    float drag = length(_Particles[pIdx].Velocity) * length(_Particles[pIdx].Velocity) * (_Drag+addedDrag) * _DeltaTime;
    _Particles[pIdx].Velocity -= normalize(_Particles[pIdx].Velocity) * drag;

    if (pIdx == _SelectedParticle)
    {
        _Debug[0] = _Particles[pIdx].Position;
        _Debug[1] = collisionNormal;
        _Debug[2] = _TempData[pIdx].DisplacementVelocity;
        _Debug[3] = _TempData[pIdx].DisplacementDensity;
        _Debug[4] = _TempData[pIdx].DisplacementCollision;
    }
    
}
